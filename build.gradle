plugins {
    id "com.jfrog.bintray" version "1.8.4"
}
apply plugin: 'java'
apply plugin: 'eclipse'
apply plugin: 'idea'
apply plugin: 'maven-publish'
apply plugin: 'maven'
apply plugin: 'osgi'

sourceCompatibility = JavaVersion.VERSION_1_8
targetCompatibility = JavaVersion.VERSION_1_8

repositories {
    mavenLocal()
    jcenter()
}

dependencies {
    compile ("com.vaadin:vaadin-server:${vaadinVersion}")
    testCompile "junit:junit:4.12"
}

if (JavaVersion.current().isJava8Compatible()) {
    allprojects {
        tasks.withType(Javadoc) {
            options.addStringOption('Xdoclint:none', '-quiet')
        }
    }
}

javadoc {
    // this fixes a javadoc issue with vaadin packaging which is quite uncommon
    options.addStringOption("sourcepath", "")
}

task createAddonPom {
    pom { artifactId = project.artifactId }.writeTo("${sourceSets.main.output.resourcesDir}/META-INF/maven/${project.group}/${project.artifactId}/pom.xml")
}

jar {
    createAddonPom
    baseName = project.artifactId
    // Include sources
//    sourceSets.main.java.srcDirs.each {
//        from it
//    }

    manifest {
        // the manifest of the default jar is of type OsgiManifest
        name = project.artifactId
        attributes "Vaadin-Package-Version": "1"
        attributes "Implementation-Title": "Chart.js Add-on"
        attributes "Implementation-Version": project.version
        attributes "Implementation-Vendor": 'Michael Oberwasserlechner'
        instruction 'Import-Package', '!com.google.gwt.*', '!com.vaadin.client.*', '*'
        instruction 'Bundle-Vendor', 'Michael Oberwasserlechner'
        instruction 'Bundle-Description', 'Chart.js Add-on'
        instruction 'Bundle-DocURL', 'https://github.com/moberwasserlechner/vaadin-chartjs'
    }
}


task vaadinDirectory(type: Zip) {

    dependsOn 'jar'
    dependsOn 'sourcesJar'
    dependsOn 'javadocJar'
    // create manifest
    mkdir 'build/libs/META-INF'
    new File("build/libs/META-INF/MANIFEST.MF").text =
"""Manifest-Version: 1.0
Vaadin-Package-Version: 1
Vaadin-Addon: """+project.artifactId+"""-"""+project.version+""".jar
Vaadin-License-Title: MIT
Implementation-Vendor: Michael Oberwasserlechner
Implementation-Title: Chart.js Add-on
Implementation-Version: """+project.version+"""
"""
    from 'build/libs'
    include '*.jar'
    include '*/*'
    destinationDir(file('build/libs/'))
}

// custom tasks for creating source/javadoc jars
task sourcesJar(type: Jar, dependsOn: classes) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from javadoc.destinationDir
}

// add javadoc/source jar tasks as artifacts
artifacts {
    archives sourcesJar, javadocJar
}

// ###################################
// ### Publish to local maven repo ###
// ###################################

publishing {
    publications {
        mavenJava(MavenPublication) {
            artifactId project.artifactId

            from components.java

            artifact sourcesJar
            artifact javadocJar
        }
    }
}

// ##########################
// ### Publish to bintray ###
// ##########################

bintray {
    user = project.hasProperty('BINTRAY_USER') ? BINTRAY_USER : null //this usually comes from gradle.properties file in ~/.gradle
    key = project.hasProperty('BINTRAY_API_KEY') ? BINTRAY_API_KEY : null //this usually comes from gradle.properties file in ~/.gradle

    // configurations = ['archives'] //When uploading configuration files
    // - OR -
    publications = ['mavenJava'] //When uploading Maven-based publication files

    dryRun = false //Whether to run this as dry-run, without deploying
    publish = false //If version should be auto published after an upload

    // Package configuration. The plugin will use the repo and name properties to check if the package already exists.
    // In that case, there's no need to configure the other package properties (like userOrg, desc, etc).
    pkg {
        repo = 'maven'
        name = 'vaadin-chartjs'
        //userOrg = 'byteowls' //An optional organization name when the repo belongs to one of the user's orgs
        desc = 'Vaadin Add-on for Chart.js chart library'
        websiteUrl = 'https://github.com/moberwasserlechner/vaadin-chartjs'
        issueTrackerUrl = 'https://github.com/moberwasserlechner/vaadin-chartjs/issues'
        vcsUrl = 'https://github.com/moberwasserlechner/vaadin-chartjs.git'
        licenses = ['MIT']
        labels = ['vaadin', 'addon', 'chartjs', 'charting']
        publicDownloadNumbers = true

        version {
            name = project.version //Bintray logical version name
            // desc = 'optional'
            vcsTag = project.version
        }
    }
}

task wrapper(type: Wrapper) {
    gradleVersion = '4.6'
}
